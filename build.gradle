/*
 * ***************************************************************************
 *   Copyright 2016-2017 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ***************************************************************************
 */

buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

allprojects {
    group = 'com.spectralogic.tpfr'
    version = '1.0.4'
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    kotlin { experimental { coroutines 'enable' } }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine_version"
        compile "org.slf4j:slf4j-api:$slf4j_version"

        testCompile "org.slf4j:slf4j-simple:$slf4j_version"
        testCompile "net.wuerl.kotlin:assertj-core-kotlin:$assertj_kotlin_version"
        testCompile "junit:junit:$junit_version"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

project(":client") {
    dependencies {
        compile project(":api")
    }
}

project(":integration") {
    dependencies {
        compile project(":client")
    }
}
